# Procedure
- PL/SQL에서 가장 대표적인 구조 
- 자주 실행해야 하는 **업무 흐름(쿼리)을 미리 작성하여 DB 내에 저장해 두었다가 필요 할 때마다 실행**
- 특정한 로직을 처리하고 결과를 반환하지 않는다. (java의 void와 비슷) 
- 선행 컴파일을 해서 속도가 빠르다.

- 특징
   - 패키지 내에서 중복 정의가 가능하다.
   - 테이블이 삭제된다고 연관된 프로시저가 삭제 되는 것은 아니지만, 삭제된 상태에서 실행하면 오류가 발생한다.
   - 프로시저 안에서 I/U/D 문 사용하는 경우 COMMIT; 이 필요 (자동 COMMIT 안됨)   
      → 끝까지 성공해야 COMMIT, 하나라도 실패하면 ROLLBACK. (트랜잭션 작업에 굉장히 용이) 

#
### 생성 및 실행 

 #### 형식
   
    CREATE [OR REPLACE] PROCEDURE 프로시저명 
    [(인수 IN|OUT|IN OUT|)]
    IS 변수 
    BEGIN 실행부 
    END;
    /
#
 #### 활용 예제  
 
      ** 다음과 같은 테이블에서 
      -------------------------------------------------
     
      CREATE TABLE test ( 
          num NUMBER PRIMARY KEY
          ,name VARCHAR2(30) NOT NULL 
          ,score NUMBER(3) NOT NULL 
          ,grade VARCHAR2(10) NOT NULL
      );

      CREATE SEQUENCE test_seq;

      SELECT * FROM tab;
      SELECT * FROM seq;

      -------------------------------------------------  
#
   - 프로시저 생성 

          CREATE PROCEDURE pInsertTest
          IS
          BEGIN
              INSERT INTO test(num, name, score, grade) VALUES 
              (test_seq.NEXTVAL, '홍길동', 80, '우');
              COMMIT;
          END; 
          /    

          SELECT * FROM user_procedures;
          SELECT * FROM user_dependencies;
          SELECT * FROM user_source;
#
   - 프로시저 실행
   
          EXEC pInsertTest;
          SELECT * FROM test;
#
   - 프로시저 수정 (IN 파라미터) 
   
          CREATE OR REPLACE PROCEDURE pInsertTest
          (
              pName VARCHAR2,           -- 파라미터는 크기 명시 X
              -- pName test.name%TYPE, 
              pScore IN NUMBER          -- IN은 생략 가능 
          )
          IS
              vgrade VARCHAR2(20); 
          BEGIN
              IF pScore>=90 THEN vgrade := '수';
              ELSIF pScore>=80 THEN vgrade := '우';
              ELSIF pScore>=70 THEN vgrade := '미';
              ELSIF pScore>=60 THEN vgrade := '양';
              ELSE vgrade := '가';
              END IF;

              INSERT INTO test(num, name, score, grade) VALUES 
              (test_seq.NEXTVAL, pName, pScore, vgrade);
              COMMIT;
          END; 
          /    
          EXEC pInsertTest('가가가', 88);
          EXEC pInsertTest('나나나', 90);

          SELECT * FROM test;

#
   - 수정 프로시저 생성
      - **RAISE_APPLICATION_ERROR(error_number, message)** 
      - 사용자 정의 예외 발생. error_number : -20999 ~ -20000
      
            CREATE OR REPLACE PROCEDURE pUpdateTest
            (
                pNum IN test.num%TYPE,
                pName IN test.name%TYPE,        
                pScore IN test.score%TYPE 
            )
            IS
                vgrade VARCHAR2(20); 
            BEGIN
                IF pScore < 0 OR pScore > 100 THEN 
                    RAISE_APPLICATION_ERROR(-20001, '점수는 0~100만 가능...');
                END IF;

                IF pScore>=90 THEN vgrade := '수';
                ELSIF pScore>=80 THEN vgrade := '우';
                ELSIF pScore>=70 THEN vgrade := '미';
                ELSIF pScore>=60 THEN vgrade := '양';
                ELSE vgrade := '가';
                END IF;

                UPDATE test SET name = pName, score=pScore, grade=vgrade 
                WHERE num = pNum;
                COMMIT;
            END; 
            / 

            EXEC pUpdateTest(1, 'vvv', 99);    
            EXEC pUpdateTest(2, 'aaa', 90);  
            SELECT * FROM test;

            EXEC pUpdateTest(2, 'aaa', 999);   -- 예외 발생 
#
   - 삭제 프로시저 생성
     - **pDeleteTest**
     - num 을 파라미터로 넘겨 받아 조건 만족 레코드 삭제 
     
            CREATE OR REPLACE PROCEDURE pDeleteTest
            (
                 pNum IN test.num%TYPE
            )
            IS    
            BEGIN      
                DELETE FROM test
                WHERE num = pNum;
                COMMIT;
            END;  
            /    

            EXEC pDeleteTest(1);
            SELECT * FROM test;
#
   - 하나의 레코드를 출력하는 프로시저 생성
   
          CREATE OR REPLACE PROCEDURE pSelectListTest
          IS    
          BEGIN 
              FOR rec IN (SELECT num,name,score,grade FROM test)  LOOP
              DBMS_OUTPUT.PUT_LINE(rec.num||':'||rec.name||':'||rec.score||':'||rec.grade);
              END LOOP;         
          END;
          /

          EXEC pSelectListTest;
#
   - 모든 레코드를 출력하는 프로시저
      
          CREATE OR REPLACE PROCEDURE pSelectOneTest 
          (
              pNum IN NUMBER 
          )
          IS 
              rec test%ROWTYPE; 
          BEGIN 
              SELECT * INTO rec FROM test WHERE num = pNum;
              DBMS_OUTPUT.PUT_LINE(rec.num||':'||rec.name||':'||rec.score||':'||rec.grade);
          END;
          /
